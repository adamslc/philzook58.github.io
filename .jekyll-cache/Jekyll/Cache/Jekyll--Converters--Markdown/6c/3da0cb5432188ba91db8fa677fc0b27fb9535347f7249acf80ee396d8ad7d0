I"Ʌ<p>Types have an algebra very analogous to the <a href="https://gist.github.com/gregberns/5e9da0c95a9a8d2b6338afe69310b945">algebra of ordinary numbers</a> (<a href="https://www.youtube.com/watch?v=YScIPA8RbVE">video</a>). This is the basic table of correspondences. Code with all the extensions available <a href="https://github.com/philzook58/lens-algebra">here</a>.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;type a * b = (a,b)
type a + b = Either a b
type b ^ a = a -&gt; b
type O = Void
type I = ()

-- check out typelits implementation
-- http://hackage.haskell.org/package/base-4.12.0.0/docs/src/GHC.TypeNats.html#%2A

infixl 6 + -- ((a + b) + c) associates to the left
infixl 7 *
infixr 8 ^


-- derived definitions
type Succ a = I + a
type Two = Succ I
type Three = Succ Two
type Four = Succ Three&lt;/code&gt;
</code></pre></div></div>

<p>One way to see that this makes sense is by counting the cardinality of types built out of these combinators. Unit is the type with 1 inhabitant. Void has 0 inhabitants. If <code class="language-plaintext highlighter-rouge">a</code> has $latex n$ and <code class="language-plaintext highlighter-rouge">b</code> has $latex m$ possible values, then <code class="language-plaintext highlighter-rouge">Either a b</code> has $latex n + m$ inhabitants, <code class="language-plaintext highlighter-rouge">(a,b)</code> has $latex n*m$ and there are $latex n^m$ possible tabulations of a-&gt;b. We’re gonna stick to just polynomials for the rest of this, ignoring a-&gt;b.</p>

<p>Another way of looking at this is if two finitely inhabited types have the same number of inhabitants, then the types can be put into an isomorphism with each other. In other words, types modulo isomorphisms can be thought as representing the natural numbers. Because of this, we can build a curious proof system for the natural numbers using ordinary type manipulation.</p>

<p>In addition, we also get a natural way of expressing and manipulating polynomials.Polymorphic types can be seen as being very similar to polynomial expressions with natural coefficients <code class="language-plaintext highlighter-rouge">N[x]</code>. The polymorphic type variables have the ability to be instantiated to any value, corresponding to evaluating the polynomial for some number.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;type ExamplePoly a = I + a + a * a * Three &lt;/code&gt;
</code></pre></div></div>

<p>The <a href="https://github.com/ekmett/lens">Lens</a> ecosystem gives some interesting combinators for manipulating this algebra. The type <code class="language-plaintext highlighter-rouge">Iso' a b</code> contains <a href="http://hackage.haskell.org/package/lens-4.17.1/docs/Control-Lens-Iso.html">isomorphisms</a>. Since we’re only considering types up to isomorphism, this <code class="language-plaintext highlighter-rouge">Iso'</code> represents equality. We can give identity isomorphisms, compose isomorphisms and reverse isomorphisms.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;type a ~~ b = Iso' a b

refl ::  a ~~ a
refl = id

compose :: (a ~~ b) -&gt; (b ~~ c) -&gt; (a ~~ c)
compose = (.)

rev :: a ~~ b -&gt; b ~~ a
rev = from&lt;/code&gt;
</code></pre></div></div>

<p>We can already form a very simple proof.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- a very simple proof. Holds by definition
oneonetwo :: (I + I) ~~ Two
oneonetwo = id&lt;/code&gt;
</code></pre></div></div>

<p>Now we’ll add some more combinators, basically the axioms that the types mod isos are a commutative <a href="https://en.wikipedia.org/wiki/Semiring">semiring</a>. Semirings have an addition and multiplication operator that distribute over each other. It is interesting to note that I believe all of these <code class="language-plaintext highlighter-rouge">Iso'</code> actually are guaranteed to be isomorphisms ( <code class="language-plaintext highlighter-rouge">to . from = id</code> and <code class="language-plaintext highlighter-rouge">from . to = id</code> ) because of <a href="https://www.well-typed.com/blog/2015/05/parametricity/">parametricity</a>. <code class="language-plaintext highlighter-rouge">from</code> and <code class="language-plaintext highlighter-rouge">to</code> are unique ignoring any issues with bottoms because the polymorphic type signature is so constraining. This is not usually guaranteed to be true in Haskell just from saying it is an <code class="language-plaintext highlighter-rouge">Iso'</code>. If I give you an <code class="language-plaintext highlighter-rouge">Iso' Bool Bool</code> it might actually be the <code class="language-plaintext highlighter-rouge">iso (const True) (const True)</code> for example, which is not an isomorphism.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- now we start to state our axioms
-- interestingly, I believe the Iso and Lens laws to follow actually follow via parametricity.

-- associativity + a identity object make mul and plus a monoid
plus_assoc :: Iso' (a + (b + c)) ((a + b) + c)
plus_assoc =  iso assoc unassoc  where
                   assoc (Left a) = Left (Left a)
                   assoc (Right (Left b)) = Left (Right b)
                   assoc (Right (Right c)) = Right c
                   unassoc (Left (Left a)) = Left a
                   unassoc (Left (Right b)) = Right (Left b)
                   unassoc (Right c) = (Right (Right c))

mul_assoc :: Iso' (a * (b * c)) ((a * b) * c)
mul_assoc =  iso (\(a,(b,c)) -&gt; ((a,b),c)) (\((a,b),c) -&gt; (a,(b,c)))


-- use `absurd` from Data.Void for empty case.
id_plus :: Iso' (a + O) a
id_plus = iso (\case Left x -&gt; x
                     Right x -&gt; absurd x) Left

id_mul :: Iso' (a * I) a
id_mul = iso (\(x,_) -&gt; x) (\x -&gt; (x,()))


-- they are also commutative
-- specialized version of swapped from Control.Lens.Iso for future clarity
comm_plus :: Iso' (a + b) (b + a)
comm_plus = swapped
comm_mul :: Iso' (a * b) (b * a)
comm_mul = swapped


-- I don't see this one in Lens. Maybe it is there though?
-- distributive property
rdist :: Iso' (a * (b + c)) (a * b + a * c)
rdist = iso (\(a,bc) -&gt; (a,) +++ (a,) $ bc) (\case Left (a,b) -&gt; (a, Left b)
                                                  Right (a,c) -&gt; (a, Right c))   

mul_zero :: Iso' (a,O) O
mul_zero = iso (\(_,y) -&gt; absurd y) absurd
&lt;/code&gt;
</code></pre></div></div>

<p>There are also combinators for <a href="http://hackage.haskell.org/package/lens-4.17.1/docs/Control-Lens-Iso.html#v:bimapping">lifting isomorphisms into bifunctors</a>: <code class="language-plaintext highlighter-rouge">firsting</code>, <code class="language-plaintext highlighter-rouge">seconding</code>, and <code class="language-plaintext highlighter-rouge">bimapping</code>. These are important for indexing into subexpressions of our types in a point-free style.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- a specialized version of firsting and seconding for clarity
-- firsting and seconding feel to me more like words for tuples
lefting :: (a ~~ b) -&gt; (a + c) ~~ (b + c)
lefting = firsting

righting :: (a ~~ b) -&gt; (c + a) ~~ (c + b)
righting = seconding&lt;/code&gt;
</code></pre></div></div>

<p>Here is a slightly more complicated proof now  available to us.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- a more complicated proof
twotwofour :: Iso' (Two + Two) Four
twotwofour = rev plus_assoc&lt;/code&gt;
</code></pre></div></div>

<p>We can attempt a more interesting and difficult proof. I developed this iteratively using <code class="language-plaintext highlighter-rouge">. _</code> hole expressions so that GHC would tell me what I had manipulated my type to at that point in my proof.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;ldist ::   ((b + c) * a) ~~ (b * a + c * a)
ldist = comm_mul . rdist . (lefting comm_mul) . (righting comm_mul)


-- very painful. Using holes _ and error messages absolutely essential
factorexample ::  ((a + I) * (a + I)) ~~ (a * a + Two * a + I)
factorexample = rdist .  -- distribute out the right side 
               (lefting (comm_mul . rdist)) . -- in the left sum term distribute out
               (righting (comm_mul . rdist)) . -- in the right sum term distribute out
                plus_assoc . -- reassociate plus to the left
               (lefting (lefting (righting comm_mul))) . -- turn a * I term to I * a
                (lefting (rev plus_assoc)) . -- associate the two a*I terms together into an (a * I + a * I) term 
                 (lefting (righting (rev ldist))) . -- factor together that subterm into Two * a
                  (righting id_mul) -- convert last I * I term into I

&lt;/code&gt;
</code></pre></div></div>

<p><img src="http://philzucker2.nfshost.com/wp-content/uploads/2019/05/haskell.gif" alt="" />Artwork Courtesy of David. Sorry for any motion sickness.</p>

<p>The proof here is actually pretty trivial and can be completely automated away. We’ll get to that later.</p>

<p>If <code class="language-plaintext highlighter-rouge">Iso'</code> is equality, what about the other members of the Lens family?  <a href="https://blog.jle.im/entry/lenses-products-prisms-sums.html">Justin Le</a> says that <code class="language-plaintext highlighter-rouge">Lens s a</code> are witness to the isomorphism of a type <code class="language-plaintext highlighter-rouge">s</code> to the tuple of something and <code class="language-plaintext highlighter-rouge">a</code>. <code class="language-plaintext highlighter-rouge">Prism</code> witness a similar thing for sums. Once we are only considering types mod isos, if you think about it, these are expressions of two familiar relations on the natural numbers: the inequality relation and the divisibility relation</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;type a &gt;= b = Prism' a b
type a .| b = Lens' a b&lt;/code&gt;
</code></pre></div></div>

<p>Mathematically, these relations can be composed with equalities, just like in the lens hierarchy <code class="language-plaintext highlighter-rouge">Lens</code> and <code class="language-plaintext highlighter-rouge">Prism</code> can be composed with <code class="language-plaintext highlighter-rouge">Iso</code>. Both form a category, since they both have <code class="language-plaintext highlighter-rouge">id</code> and <code class="language-plaintext highlighter-rouge">(.)</code>.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;{-

the core combinators from the lens library for manipulating these are

_1 :: (a * b) .| a
_2 :: (a * b) .| b

_Left :: (a + b) &gt;= a
_Right :: (a + b) &gt;= b

For example:
-}
twodiv4 :: (Two * Two) .| Two
twodiv4 = _1

onelesstwo :: Two &gt;= I
onelesstwo = _Left

threedivthree :: Three .| Three
threedivthree = id
&lt;/code&gt;
</code></pre></div></div>

<p><del>Here are a couple identities that we can’t derive from these basic combinators. There are probably others.</del> Woah-ho, my bad. These are totally derivable using id_mul, id_plus, mul_zero, _1, _2, _Left, _Right.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- once again, these are true via parametricity, more or less
one_div :: a .| I 
-- one_div = lens (const ()) const
one_div = (rev id_mul) . _2

zero_lte :: a &gt;= O
-- zero_lte = prism' absurd (const Nothing)
zero_lte = (rev id_plus) . _Right

zero_div :: O .| a
-- zero_div = lens absurd const
zero_div = (rev mul_zero) . _1&lt;/code&gt;
</code></pre></div></div>

<p>Pretty neat! Random thoughts and questions before we get into the slog of automation:</p>

<ul>
  <li>Traversal is the “is polynomial in” relation, which seems a rather weak statement on it’s own.</li>
  <li>Implementing automatic polynomial division is totally possible and interesting</li>
  <li>What is the deal with infinite types like <code class="language-plaintext highlighter-rouge">[a]</code>? <code class="language-plaintext highlighter-rouge">Fix</code>. I suppose this is a theory of <a href="https://en.wikipedia.org/wiki/Formal_power_series">formal power series</a> / combinatorial species. Fun combinatorics, generatingfunctionology. <a href="https://byorgey.wordpress.com/2012/11/20/combinatorial-species-definition/">Brent Yorgey</a> did his <a href="https://www.cis.upenn.edu/~sweirich/papers/yorgey-thesis.pdf">dissertation</a> on this stuff. Wow. I’ve never really read this. It is way more relevant than I realized.</li>
  <li>Multivariate polynomial algorithms would also be interesting to explore (Grobner basis, multivariate division)</li>
  <li>Derivatives of types and zippers - <a href="http://strictlypositive.org/calculus/">Conor McBride</a></li>
  <li>Negative Numbers and Fractions?</li>
  <li>
    <p>Lifting to rank-1 types. Define Negative and Fractions via <a href="https://www.sciencedirect.com/science/article/pii/S1567832612000525">Galois connection?</a></p>

    <p><code>-- partially applied +
type P n = (Either n) 
-- partially applied *
type M n = (,) n</code></p>
  </li>
</ul>

<p>Edit: /u/carette (wonder who that is ;) ) writes:</p>

<p>“You should dig into</p>

<p>J Carette, A Sabry <a href="http://www.cas.mcmaster.ca/%7Ecarette/publications/esop2.pdf">Computing with semirings and weak rig groupoids</a>, in Proceedings of ESOP 2016, p. 123-148. Agda code in <a href="https://github.com/JacquesCarette/pi-dual/tree/master/Univalence">https://github.com/JacquesCarette/pi-dual/tree/master/Univalence</a>. A lot of the algebra you develop is there too.</p>

<p>If you hunt around in my repos, you’ll also find things about lenses, exploring some of the same things you mention here.”</p>

<p>Similar ideas taken further and with more sophistication. Very interesting. Check it out.</p>

<h2 id="automation">Automation</h2>

<p>Our factor example above was quite painful, yet the theorem was exceedingly obvious by expansion of the left and right sides. Can we automate that? Yes we can!</p>

<p>Here’s the battle plan:</p>

<ul>
  <li>Distribute out all expressions like $latex a*(b+c)$ so that all multiplication nodes appear at the bottom of the tree.</li>
  <li>Reduce the expression by absorbing all stupid $latex a<em>1$, $latex a</em>0$, $latex a+0$ terms.</li>
  <li>Reassociate everything to the right, giving a list like format</li>
  <li>Sort the multiplicative terms by power of the variable</li>
</ul>

<p>Once we have these operations, we’ll combine them into a <code class="language-plaintext highlighter-rouge">canonical</code> operation. From there, most equality proofs can be performed using the <code class="language-plaintext highlighter-rouge">rewrite</code> operation, which merely puts both sides into canonical form</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- | The main combinator to expand out any polynomial into canonical form with terms sorted

canonical :: (Dist a b, Absorb b c, RightAssoc c d, SortTerm d e) =&gt; a ~~ e
canonical = dist . absorb . rightAssoc . sortTerm

rewrite :: forall a' a b c d e e' d' c' b'. (Dist a b, Absorb b c, RightAssoc c d,
                 SortTerm d e, e ~ e', Dist a' b',
                Absorb b' c', RightAssoc c' d', SortTerm d' e') =&gt; a ~~ a'
rewrite = canonical . (rev canonical)&lt;/code&gt;
</code></pre></div></div>

<p>Once we have those, the painful theorem above and harder ones becomes trivial.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;ex9 :: (a ~ V a') =&gt; ((a + I) * (I + a)) ~~ (a * a + Two * a + I)
ex9 = rewrite 

ex10 :: (a ~ V a') =&gt; ((a + I) * (I + a) * (Two + a)) ~~  (a * a * a + a * a * Four + Two + Four * a + a)
ex10 = rewrite &lt;/code&gt;
</code></pre></div></div>

<p>Now we’ll build the Typeclasses necessary to achieve each of these aims in order. The Typeclass system is perfect for what we want to do, as it builds terms by inspecting types. It isn’t perfect in the sense that typeclass pattern matching needs to be tricked into doing what we need. I have traded in cleverness and elegance with verbosity.</p>

<p>In order to make our lives easier, we’ll need to tag every variable name with a newtype wrapper. Otherwise we won’t know when we’ve hit a leaf node that is a variable. I’ve used this trick before <a href="http://www.philipzucker.com/shit-compiling-categories-using-type-level-programming-haskell/">here</a> in an early version of my faking Compiling to Categories series. These wrappers are easily automatically stripped.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;-- For working with Variables

-- a newtype to mark variable position
newtype V a = V a

-- Suggested usage, bind the V in a unification predicate to keep expression looking clean
-- (V a' ~ a) =&gt; (a + I) * a 

-- multinomials. to be implemented some other day
-- a phantom l labelled newtype for variable ordering. 
newtype VL l a = VL a&lt;/code&gt;
</code></pre></div></div>

<p>A common pattern I exploit is to use a type family to drive complicated recursion. Closed type families allow more overlap and default patterns which is very useful for programming. However, type families do not carry values, so we need to flip flop between the typeclass and type family system to achieve our ends.</p>

<p>Here is the implementation of the distributor <code class="language-plaintext highlighter-rouge">Dist</code>. We make <code class="language-plaintext highlighter-rouge">RDist</code> and <code class="language-plaintext highlighter-rouge">LDist</code> typeclasses that make a sweep of the entire tree, using <code class="language-plaintext highlighter-rouge">ldist</code> and <code class="language-plaintext highlighter-rouge">rdist</code> as makes sense. It was convenient to separate these into two classes for my mental sanity. I am not convinced even now that I have every case. Then the master control class <code class="language-plaintext highlighter-rouge">Dist</code> runs these classes until any node that has a <code class="language-plaintext highlighter-rouge">(*)</code> in it has no nodes with <code class="language-plaintext highlighter-rouge">(+)</code> underneath, as checked by the <code class="language-plaintext highlighter-rouge">HasPlus</code> type family.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;
class RDist a b | a -&gt; b where
    rdist' :: Iso' a b

instance RDist a a' =&gt; RDist (a * I) (a' * I) where
    rdist' = firsting rdist'
instance RDist a a' =&gt; RDist (a * O) (a' * O) where
    rdist' = firsting rdist'
instance RDist a a' =&gt; RDist (a * (V b)) (a' * (V b)) where
    rdist' = firsting rdist'

instance (RDist (a * b) ab, RDist (a * c) ac) =&gt; RDist (a * (b + c)) (ab + ac) where
    rdist' = rdist . (bimapping rdist' rdist')
instance (RDist a a', RDist (b * c) bc) =&gt; RDist (a * (b * c)) (a' * bc) where
    rdist' = (bimapping rdist' rdist')
instance (RDist a a', RDist b b') =&gt; RDist (a + b) (a' + b') where
    rdist' = bimapping rdist' rdist'

instance RDist O O where
    rdist' = id
instance RDist I I where
    rdist' = id
instance RDist (V a) (V a) where
    rdist' = id

-- can derive ldist from swapped . rdist' . swapped?

class LDist a b | a -&gt; b where
    ldist' :: Iso' a b
instance (LDist (b * a) ab, LDist (c * a) ac) =&gt; LDist ((b + c) * a) (ab + ac) where
    ldist' = ldist . (bimapping ldist' ldist')
instance (LDist (b * c) bc, LDist a a') =&gt; LDist ((b * c) * a) (bc * a') where
    ldist' = bimapping ldist' ldist'
instance LDist a a' =&gt; LDist (I * a) (I * a') where
    ldist' = seconding ldist'
instance LDist a a' =&gt; LDist (O * a) (O * a') where
    ldist' = seconding ldist'
instance LDist a a' =&gt; LDist ((V b) * a) ((V b) * a') where
    ldist' = seconding ldist'

instance (LDist a a', LDist b b') =&gt; LDist (a + b) (a' + b') where
    ldist' = bimapping ldist' ldist'

instance LDist O O where
    ldist' = id
instance LDist I I where
    ldist' = id
instance LDist (V a) (V a) where
    ldist' = id

type family HasPlus a where
    HasPlus (a + b) = 'True
    HasPlus (a * b) = (HasPlus a) || (HasPlus b)
    HasPlus I = 'False
    HasPlus O = 'False
    HasPlus (V _) = 'False

class Dist' f a b | f a -&gt; b where
    dist' :: a ~~ b
instance (f ~ HasPlus ab', LDist (a * b) ab, 
          RDist ab ab', Dist' f ab' ab'') =&gt; Dist' 'True (a * b) ab'' where
    dist' = ldist' . rdist' . (dist' @f)
instance Dist' 'False (a * b) (a * b) where
    dist' = id
instance (HasPlus a ~ fa, HasPlus b ~ fb, 
          Dist' fa a a', Dist' fb b b') =&gt; Dist' x (a + b) (a' + b') where
    dist' = bimapping (dist' @fa) (dist' @fb)
    -- is that enough though? only dist if 
instance Dist' x I I where
    dist' = id
instance Dist' x O O where
    dist' = id
instance Dist' x (V a) (V a) where
    dist' = id

class Dist a b | a -&gt; b where
    dist :: a ~~ b
-- dist' should distributed out all multiplications
instance (f ~ HasPlus a, Dist' f a b) =&gt; Dist a b where
    dist = dist' @f
&lt;/code&gt;
</code></pre></div></div>

<p>Next is the <code class="language-plaintext highlighter-rouge">Absorb</code> type class. It is arranged somewhat similarly to the above. Greedily absorb, and keep doing it until no absorptions are left. I think that works.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;
-- RAbsorb matches only on the right hand side of binary operators
-- matching on both sides is ungainly to write
class RAbsorb a b | a -&gt; b where
    rabsorb :: a ~~ b
-- obvious absorptions
instance RAbsorb x x' =&gt; RAbsorb (x + O) x' where
    rabsorb = id_plus . rabsorb
instance RAbsorb x x' =&gt; RAbsorb (x + I) (x' + I) where
    rabsorb = firsting rabsorb
instance RAbsorb x x' =&gt; RAbsorb (x * I) x' where
    rabsorb = id_mul . rabsorb
instance RAbsorb (x * O) O where
    rabsorb = mul_zero
instance RAbsorb x x' =&gt; RAbsorb (x * (V a)) (x' * (V a)) where
    rabsorb = firsting rabsorb
instance RAbsorb x x' =&gt; RAbsorb (x + (V a)) (x' + (V a)) where
    rabsorb = lefting rabsorb
-- recursion steps
instance (RAbsorb (y * z) yz, RAbsorb x x') =&gt; RAbsorb (x * (y * z)) (x' * yz) where
    rabsorb = bimapping rabsorb rabsorb
instance (RAbsorb (y + z) yz, RAbsorb x x') =&gt; RAbsorb (x * (y + z)) (x' * yz) where
    rabsorb = bimapping rabsorb rabsorb
instance (RAbsorb (y + z) yz, RAbsorb x x') =&gt; RAbsorb (x + (y + z)) (x' + yz) where
    rabsorb = bimapping rabsorb rabsorb
instance (RAbsorb (y * z) yz, RAbsorb x x') =&gt; RAbsorb (x + (y * z)) (x' + yz) where
    rabsorb = bimapping rabsorb rabsorb
-- base cases
instance RAbsorb O O where
    rabsorb = id
instance RAbsorb I I where
    rabsorb = id
instance RAbsorb (V a) (V a) where
    rabsorb = id


-- mirror of RAbsorb    
class LAbsorb a b | a -&gt; b where
    labsorb :: a ~~ b
instance LAbsorb x x' =&gt; LAbsorb (O + x) x' where
    labsorb = comm_plus . id_plus . labsorb
instance LAbsorb x x' =&gt; LAbsorb (I + x) (I + x') where
    labsorb = righting labsorb
instance LAbsorb x x' =&gt; LAbsorb (I * x) x' where
    labsorb = comm_mul . id_mul . labsorb
instance LAbsorb (O * x) O where
    labsorb = comm_mul . mul_zero
instance LAbsorb x x' =&gt; LAbsorb ((V a) + x) ((V a) + x') where
    labsorb = righting labsorb
instance LAbsorb x x' =&gt; LAbsorb ((V a) * x) ((V a) * x') where
    labsorb = seconding labsorb

instance (LAbsorb (y * z) yz, LAbsorb x x') =&gt; LAbsorb ((y * z) * x) (yz * x') where
    labsorb = bimapping labsorb labsorb
instance (LAbsorb (y + z) yz, LAbsorb x x') =&gt; LAbsorb ((y + z) * x) (yz * x') where
    labsorb = bimapping labsorb labsorb
instance (LAbsorb (y + z) yz, LAbsorb x x') =&gt; LAbsorb ((y + z) + x) (yz + x') where
    labsorb = bimapping labsorb labsorb
instance (LAbsorb (y * z) yz, LAbsorb x x') =&gt; LAbsorb ((y * z) + x) (yz + x') where
    labsorb = bimapping labsorb labsorb

instance LAbsorb O O where
    labsorb = id
instance LAbsorb I I where
    labsorb = id
instance LAbsorb (V a) (V a) where
    labsorb = id
    
-- labsorb :: (Swapped p, RAbsorb (p b a) (p b' a')) =&gt; (p a b) ~~ (p a' b')
-- labsorb = swapped . rabsorb . swapped   

-- a test function to see if an expression is properly absorbed
type family Absorbed a where
    Absorbed (O + a) = 'False
    Absorbed (a + O) = 'False
    Absorbed (a * I) = 'False
    Absorbed (I * a) = 'False
    Absorbed (a * O) = 'False
    Absorbed (O * a) = 'False
    Absorbed (a + b) = (Absorbed a) &amp;&amp; (Absorbed b)
    Absorbed (a * b) = (Absorbed a) &amp;&amp; (Absorbed b)
    Absorbed I = 'True
    Absorbed O = 'True
    Absorbed (V a) = 'True

-- iteratively rabsorbs and leftabsorbs until tree is properly absorbed.
class Absorb' f a b | f a -&gt; b where
    absorb' :: a ~~ b
instance Absorb' 'True a a where
    absorb' = id
instance (LAbsorb a a', RAbsorb a' a'',
          f ~ Absorbed a'', Absorb' f a'' a''') =&gt; Absorb' 'False a a''' where
    absorb' = labsorb . rabsorb . (absorb' @f)

-- wrapper class to avoid the flag.
class Absorb a b | a -&gt; b where
    absorb :: a ~~ b
instance (f ~ Absorbed a, Absorb' f a b) =&gt; Absorb a b where
    absorb = absorb' @f
&lt;/code&gt;
</code></pre></div></div>

<p>The Associators are a little simpler. You basically just look for the wrong association pattern and call <code class="language-plaintext highlighter-rouge">plus_assoc</code> or <code class="language-plaintext highlighter-rouge">mul_assoc</code> until they don’t occur anymore, then recurse. We can be assured we’re always making progress if we either switch some association structure or recurse into subparts.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;
class LeftAssoc a b | a -&gt; b where
    leftAssoc :: Iso' a b

instance LeftAssoc a a' =&gt; LeftAssoc (a + I) (a' + I) where
    leftAssoc = firsting leftAssoc 
instance LeftAssoc a a' =&gt; LeftAssoc (a + O) (a' + O) where
    leftAssoc = firsting leftAssoc 
instance LeftAssoc a a' =&gt; LeftAssoc (a * I) (a' * I) where
    leftAssoc = firsting leftAssoc 
instance LeftAssoc a a' =&gt; LeftAssoc (a * O) (a' * O) where
    leftAssoc = firsting leftAssoc 
instance LeftAssoc a a' =&gt; LeftAssoc (a * (V b)) (a' * (V b)) where
    leftAssoc = firsting leftAssoc 
instance LeftAssoc a a' =&gt; LeftAssoc (a + (V b)) (a' + (V b)) where
    leftAssoc = firsting leftAssoc 

instance (LeftAssoc ((a + b) + c) abc') =&gt; LeftAssoc (a + (b + c)) abc' where
    leftAssoc = plus_assoc . leftAssoc 
instance (LeftAssoc ((a * b) * c) abc') =&gt; LeftAssoc (a * (b * c)) abc' where
    leftAssoc = mul_assoc . leftAssoc 


instance (LeftAssoc (b * c) bc, LeftAssoc a a') =&gt; LeftAssoc (a + (b * c)) (a' + bc) where
    leftAssoc = bimapping leftAssoc leftAssoc
-- a * (b + c) -&gt;  a * b + a * c 
-- This case won't happen if we've already distribute out.
instance (LeftAssoc (b + c) bc, LeftAssoc a a') =&gt; LeftAssoc (a * (b + c)) (a' * bc) where
    leftAssoc = bimapping leftAssoc leftAssoc

instance LeftAssoc O O where
    leftAssoc = id
instance LeftAssoc I I where
    leftAssoc = id
instance LeftAssoc (V a) (V a) where
    leftAssoc = id




-- right assoc will completely associate strings of + or -. Mixed terms are not associated.
-- cases  on left hand side of binary expression
-- always makes progress by either reassociating or recursing
class RightAssoc a b | a -&gt; b where
    rightAssoc :: Iso' a b
instance (RightAssoc (a + (b + c)) abc') =&gt; RightAssoc ((a + b) + c) abc' where
    rightAssoc = (rev plus_assoc) . rightAssoc 
instance (RightAssoc (a * (b * c)) abc') =&gt; RightAssoc ((a * b) * c) abc' where
    rightAssoc = (rev mul_assoc) . rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc (I + a) (I + a') where
    rightAssoc = seconding rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc (O + a) (O + a') where
    rightAssoc = seconding rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc (I * a) (I * a') where
    rightAssoc = seconding rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc (O * a) (O * a') where
    rightAssoc = seconding rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc ((V b) + a) ((V b) + a') where
    rightAssoc = seconding rightAssoc 
instance RightAssoc a a' =&gt; RightAssoc ((V b) * a) ((V b) * a') where
    rightAssoc = seconding rightAssoc 


instance (RightAssoc (b * c) bc, RightAssoc a a') =&gt; RightAssoc ((b * c) + a) (bc + a') where
    rightAssoc = bimapping rightAssoc rightAssoc
instance (RightAssoc (b + c) bc, RightAssoc a a') =&gt; RightAssoc ((b + c) * a) (bc * a') where
    rightAssoc = bimapping rightAssoc rightAssoc

instance RightAssoc O O where
    rightAssoc = id
instance RightAssoc I I where
    rightAssoc = id
instance RightAssoc (V a) (V a) where
    rightAssoc = id
&lt;/code&gt;
</code></pre></div></div>

<p>Finally, the <code class="language-plaintext highlighter-rouge">SortTerm</code> routine. <code class="language-plaintext highlighter-rouge">SortTerm</code> is a bubble sort. The typeclass <code class="language-plaintext highlighter-rouge">Bubble</code> does a single sweep of swapping down the type level list-like structure we’ve built. The <code class="language-plaintext highlighter-rouge">SortTerm</code> uses the <code class="language-plaintext highlighter-rouge">Sorted</code> type family to check if it is finished. If it isn’t, it call <code class="language-plaintext highlighter-rouge">Bubble</code> again.</p>

<div class="language-plaintext highlighter-rouge"><div class="highlight"><pre class="highlight"><code>&lt;code&gt;
type family (a :: k) == (b :: k) :: Bool where
    f a == g b = f == g &amp;&amp; a == b
    a == a = 'True
    _ == _ = 'False

type family SortedTerm a :: Bool where
    SortedTerm (a + (b + c)) = (((CmpTerm a b) == 'EQ) || ((CmpTerm a b) == 'GT)) &amp;&amp; (SortedTerm (b + c))
    SortedTerm (a + b) = ((CmpTerm a b) == 'EQ) || ((CmpTerm a b) == 'GT)
    --SortedTerm a = 'True 
    SortedTerm I = 'True 
    SortedTerm O = 'True 
    SortedTerm (V a) = 'True 

-- higher powers of V are bigger.
-- CmpTerm compares TimesLists.    
type family CmpTerm a b where
    CmpTerm ((V a) * b) ((V a) * c) = CmpTerm b c
    CmpTerm ((V a) * b) (V a) = 'GT
    CmpTerm (V a) ((V a) * b)  = 'LT
    CmpTerm I ((V a) * b) = 'LT
    CmpTerm ((V a) * b) I = 'GT
    CmpTerm (V a) (V a) = 'EQ
    CmpTerm I (V a) = 'LT
    CmpTerm (V a) I = 'GT
    CmpTerm I I = 'EQ


-- Maybe this is all uneccessary since we'll expand out and abosrb to a*a + a*a + a + a + a + a


-- type a == b = TEq.(==) a b


-- Head and Tail of PlusLists
type family PlusHead a where
    PlusHead (x + y) = x
    PlusHead x = x
type family PlusTail a where
    PlusTail (x + y) = y



-- bubble assume a plusList of multiplicative terms. I.E. fully distributed, fully rightassociated , fully absorbed  
-- does one pass of a bubble sort
class Bubble f a b | f a -&gt; b where
    bubble :: a ~~ b
-- more to go
instance (f ~ CmpTerm b (PlusHead c), Bubble f (b + c) bc) =&gt; Bubble 'EQ (a + (b + c)) (a + bc) where
    bubble = righting (bubble @f)
instance (f ~ CmpTerm b (PlusHead c), Bubble f (b + c) bc) =&gt; Bubble 'GT (a + (b + c)) (a + bc) where
    bubble = righting (bubble @f)
instance (f ~ CmpTerm a (PlusHead c), Bubble f (a + c) ac) =&gt; Bubble 'LT (a + (b + c)) (b + ac) where
    bubble = plus_assoc . (lefting comm_plus) . (rev plus_assoc) . righting (bubble @f)


-- The times, or constants shows that we're at the end of our + list.
instance Bubble 'EQ (a + (b * c)) (a + (b * c)) where
    bubble = id
instance Bubble 'GT (a + (b * c)) (a + (b * c)) where
    bubble = id
instance Bubble 'LT (a + (b * c)) ((b * c) + a) where
    bubble = comm_plus

instance Bubble 'EQ (a + I) (a + I) where
    bubble = id
instance Bubble 'GT (a + I) (a + I) where
    bubble = id
instance Bubble 'LT (a + I) (I + a) where
    bubble = comm_plus

instance Bubble 'EQ (a + O) (a + O) where
    bubble = id
instance Bubble 'GT (a + O) (a + O) where
    bubble = id
instance Bubble 'LT (a + O) (O + a) where -- shouldn't happen
    bubble = comm_plus

instance Bubble 'EQ (a + (V b)) (a + (V b)) where
    bubble = id
instance Bubble 'GT (a + (V b)) (a + (V b)) where
    bubble = id
instance Bubble 'LT (a + (V b)) ((V b) + a) where
    bubble = comm_plus
-- goofy base cases in case bubble gets called on a single element
instance Bubble x O O where
    bubble = id
instance Bubble x I I where
    bubble = id
instance Bubble x (V a) (V a) where
    bubble = id


-- sort term assumes rightassociated, fully distributed, fully I O absorbed expressions
class SortTerm' f a b | f a -&gt; b where -- f is flag whether PlusList is sorted.
    sortTerm' :: a ~~ b
instance SortTerm' 'True a a where
    sortTerm' = id
-- a single term with no plus shouldn't get here. That is why PlusTail is ok.
instance (f ~ CmpTerm (PlusHead a) (PlusHead (PlusTail a)), Bubble f a a',
          f' ~ SortedTerm a', SortTerm' f' a' b) =&gt; SortTerm' 'False a b where
    sortTerm' = (bubble @f) . (sortTerm' @f')
class SortTerm a b | a -&gt; b where -- f is flag whether PlusList is sorted.
    sortTerm :: a ~~ b
instance (f ~ SortedTerm a, SortTerm' f a a') =&gt; SortTerm a a' where
    sortTerm = sortTerm' @f
&lt;/code&gt;
</code></pre></div></div>

<p>Hope you thought this was neat!</p>

:ET